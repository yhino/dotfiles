# vim:ft=zsh

# to avoid overwritten PATH via /usr/libexec/path_helper
# refs. https://github.com/Homebrew/homebrew-core/pull/32074#issuecomment-421381869
unsetopt GLOBAL_RCS

typeset -xT SUDO_PATH sudo_path
typeset -U sudo_path
sudo_path=({/usr/local,/usr,}/sbin(N-/))

typeset -gx -U path
path=( \
    ~/bin(N-/) \
    ~/.local/bin(N-/) \
    /Applications/Docker.app/Contents/Resources/bin(N-/) \
    ${path} \
    ${sudo_path} \
    )

typeset -gx -U fpath
fpath=( \
    ~/.zsh/Completion(N-/) \
    ~/.zsh/functions/*(N-/) \
    ~/.zsh/plugins/zsh-completions(N-/) \
    /usr/local/share/zsh/site-functions(N-/) \
    ${fpath} \
    )

export XDG_CONFIG_HOME=${HOME}/.config
export XDG_CACHE_HOME=${HOME}/.cache

export LANGUAGE='en_US.UTF-8'
export LANG=${LANGUAGE}
export LC_ALL=${LANGUAGE}
export LC_CTYPE=${LANGUAGE}

export LESSCHARSET=utf-8
export JLESSCHARSET=japanese
export LESS='-qR'

export GREP_OPTIONS='--color=auto'
export GREP_COLOR='1;41;37'

export LSCOLORS=gxfxcxdxbxegedabagacad
if [[ -f "${HOME}/.dir_colors" ]]; then
    if which gdircolors > /dev/null; then
        eval `gdircolors -b ${HOME}/.dir_colors`
    elif which dircolors > /dev/null; then
        eval `dircolors -b ${HOME}/.dir_colors`
    fi
fi

if type nvim &>/dev/null; then
    export EDITOR=nvim
else
    export EDITOR=vim
fi

export FIGNORE=${FIGNORE}:.svn:.git

# Oracle
export NLS_LANG=JAPANESE_JAPAN.UTF8
export NLS_DATE_FORMAT="YYYY-MM-DD HH24:MI:SS"
export SQLPATH=${HOME}/.oracle

# GOLANG
export GOPATH=$HOME/go
export GOBIN=${GOPATH}/bin
path=(${GOBIN}(N-/) ${path})

# Java
if [[ -x /usr/libexec/java_home ]]; then
    export JAVA_HOME=$(/usr/libexec/java_home)
fi

# Markdown
export GLAMOUR_STYLE=dark

# Homebrew
case "$(uname -m)" in
    x86*)
        [[ -x "/usr/local/bin/brew" ]] && {
            eval "$(/usr/local/bin/brew shellenv)"
        }
        ;;
    arm*)
        [[ -x "/opt/homebrew/bin/brew" ]] && {
            eval "$(/opt/homebrew/bin/brew shellenv)"
        }
        ;;
esac


# localize
[[ -f ${HOME}/.zshenv.local ]] && {
    source ${HOME}/.zshenv.local
}
